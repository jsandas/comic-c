; Gimp Script-Fu to produce transparent map overlay graphics.
; (gimp-display-new (map-overlay filenames 2))

(define (map-overlay filenames shift)
  (define (range start end)
    (if (= start end)
        '()
        (cons start (range (+ 1 start) end))))

  (let ((image (car (gimp-file-load RUN-NONINTERACTIVE (car filenames) (car filenames)))))
    (gimp-image-undo-disable image)
    (for-each (lambda (filename)
                (let ((layer (car (gimp-file-load-layer RUN-NONINTERACTIVE image filename))))
                  (gimp-layer-set-opacity layer 80) ; background layer stays at 100% opacity
                  (gimp-image-insert-layer image layer 0 -1)))
              (cdr filenames))
    (let* ((layers (vector->list (cadr (gimp-image-get-layers image))))
           (num-layers (length layers))
           ; color palette from viridis
           ; https://cran.r-project.org/web/packages/viridis/vignettes/intro-to-viridis.html#the-color-scales
           (palette (cond ((= num-layers 2) '((68 51 119) (136 221 68)))
                          ((= num-layers 3) '((68 51 119) (34 136 136) (136 221 68)))
                          (else (error "no palette for this number of layers")))))
      (gimp-image-resize image
                         (+ (car (gimp-image-width image))
                            (* shift (- (length layers) 1)))
                         (+ (car (gimp-image-height image))
                            (* shift (- (length layers) 1)))
                         0 0)
      (for-each (lambda (layer i color)
                  (gimp-layer-add-alpha layer)
                  (gimp-image-select-color image CHANNEL-OP-REPLACE layer '(0 0 0))
                  (gimp-context-set-foreground color)
                  (gimp-drawable-edit-fill layer FILL-FOREGROUND)
                  (gimp-selection-invert image)
                  (gimp-drawable-edit-clear layer)
                  (gimp-item-transform-translate layer (* i shift) (* i shift)))
                (reverse layers)
                (range 0 (length layers))
                palette))
    (gimp-selection-none image)
    (gimp-image-clean-all image)
    (gimp-image-undo-enable image)
    image))
